apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

version = "1.3"

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 27
        versionCode 4
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
}

task deleteJar(type: Delete) {
    delete 'libs/jars/sectionedrecyclerviewlib.jar'
}

task createJar(type: Copy) {
    from('build/intermediates/intermediate-jars/release/')
    into('libs/jars/')
    include('classes.jar')
    rename('classes.jar', 'sectionedrecyclerviewlib.jar')
}

createJar.dependsOn(deleteJar, build)

def siteUrl = 'https://github.com/JoaoMarra/SectionedRecyclerView'      // Homepage URL of the library
def gitUrl = 'https://github.com/JoaoMarra/SectionedRecyclerView.git'   // Git repository URL
group = "br.marraware"                                          // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'br.marraware.sectionedRecyclerView'
                description = 'https://github.com/JoaoMarra/SectionedRecyclerView'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'joaomarra'
                        name 'Jo√£o Gabriel Silva Marra'
                        email 'silvamarraster@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
File propFile = "${System.properties['user.home']}${File.separator}.gradle${File.separator}bintray.properties" as File
properties.load(propFile.newDataInputStream())

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "SectionedRecyclerView"
        // it is the name that appears in bintray when logged
        name = "br.marraware.sectionedRecyclerView"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
